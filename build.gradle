buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'eclipse'
apply plugin: 'idea'

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        project.file("exclude.txt").eachLine {
            line ->
                println line
                if (!line.startsWith("#")) {
                    exclude line.trim()
                }
        }
    }


    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    if (System.getenv("KEYSTORE") != null) {
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword new String(System.console().readPassword("\n\$ Keystore password: "))
                keyAlias "aard2-android"
                keyPassword new String(System.console().readPassword("\n\$ Key password: "))
            }
        }
    }

    buildTypes {
        if (System.getenv("KEYSTORE") != null) {
            release {
                signingConfig signingConfigs.release
            }
        }
    }
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4+'
    compile 'com.fasterxml.jackson.core:jackson-core:2.4+'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.4+'
    compile project(':slobj')
    compile project(':slobber')
    compile project(':slobber').files('libs/simple-5.1.7.1.itkach.jar')
    compile project(':fdrawable')
    compile files('libs/android-support-v13.jar')
}
